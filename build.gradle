plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "1.8.5"
}

description = "Jefferson Lab logbook API"
group 'org.jlab'
version = '4.0.0'

ext {
    releaseDate = 'Sep 24 2020'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

processResources {
    filesMatching('release.properties') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}

javadoc {
    options.overview = "src/main/java/overview.html"
    options.source = 8
    options.with {
        links 'https://docs.oracle.com/javase/8/docs/api'
    }
}

// We only automatically run tests that end with "Test".  That way, we can define
// integration tests or other special tests that would fail in Travis, but we can manually
// execute in special environments (Docker anyone).
test {
    include '**/*Test.class'
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "slominskir"
            name "Ryan Slominski"
            email "ryans@jlab.org"
        }
    }

    scm {
        url "https://github.com/JeffersonLab/jlog"
    }
}

publishing {
    publications {
        ProjectPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId 'jlog'
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Jefferson Lab Java logbook API')
                root.appendNode('name', 'jlog')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['ProjectPublication']

    pkg {
        repo = 'maven'
        name = 'jlog'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/JeffersonLab/jlog.git'
        version {
            name = '4.0.0'
        }
    }
}
